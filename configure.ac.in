dnl
dnl Docklike Taskbar - A modern, minimalist taskbar for Xfce
dnl --------------------------------------------------------
dnl (c) 2019-2020		Nicolas Szabo <nszabo@vivaldi.net>
dnl (c) 2020-2021		David Keogh <davidtkeogh@gmail.com>
dnl

dnl ***************************
dnl *** Version information ***
dnl ***************************
m4_define([project_version_major], [0])
m4_define([project_version_minor], [4])
m4_define([project_version_micro], [1])
m4_define([project_version_build], [@REVISION@])
m4_define([project_version_tag], [git]) # leave empty for release
m4_define([project_version], [project_version_major().project_version_minor().project_version_micro()ifelse(project_version_tag(), [git], [project_version_tag()-project_version_build()], [project_version_tag()])])

dnl ***************************
dnl *** Initialize autoconf ***
dnl ***************************
AC_COPYRIGHT([Copyright (c) 2019-2023 The Xfce development team. All rights reserved.
  Written for Xfce by Nicolas Szabo <nszabo@vivaldi.net> and David Keogh <davidtkeogh@gmail.com>.])
AC_INIT([xfce4-docklike-plugin], [project_version], [https://gitlab.xfce.org/panel-plugins/xfce4-docklike-plugin])
AC_PREREQ([2.69])
AC_CANONICAL_TARGET()
AC_REVISION([project_version_build])
AC_CONFIG_MACRO_DIRS([m4])

dnl ***************************
dnl *** Initialize automake ***
dnl ***************************
AM_INIT_AUTOMAKE([1.8 no-dist-gzip dist-bzip2 tar-ustar foreign])
AM_MAINTAINER_MODE()
m4_ifdef([AM_SILENT_RULES], [AM_SILENT_RULES([yes])])

dnl **************************
dnl *** Initialize libtool ***
dnl **************************
LT_PREREQ([2.2.6])
LT_INIT([disable-static])

dnl ********************************
dnl *** Check for basic programs ***
dnl ********************************
AC_PROG_CXX()
AC_PROG_INSTALL()
AC_PROG_LN_S()
IT_PROG_INTLTOOL([0.35.0])

dnl ***********************************
dnl *** Check for required packages ***
dnl ***********************************
m4_define([xfce_build_version], [4.14.0])
XDT_CHECK_LIBX11_REQUIRE()
XDT_CHECK_PACKAGE([GIO], [gio-2.0], [2.58.0])
XDT_CHECK_PACKAGE([GLIB], [glib-2.0], [2.58.0])
XDT_CHECK_PACKAGE([GTK], [gtk+-3.0], [3.24.0])
XDT_CHECK_PACKAGE([CAIRO], [cairo], [1.16.0])
XDT_CHECK_PACKAGE([WNCK], [libwnck-3.0], [3.30.0])
XDT_CHECK_PACKAGE([X11], [x11], [1.6])
XDT_CHECK_PACKAGE([LIBXFCE4UI], [libxfce4ui-2], [xfce_build_version()])
XDT_CHECK_PACKAGE([LIBXFCE4UTIL], [libxfce4util-1.0], [xfce_build_version()])
XDT_CHECK_PACKAGE([LIBXFCE4PANEL], [libxfce4panel-2.0], [xfce_build_version()])

dnl ******************************
dnl *** Check for i18n support ***
dnl ******************************
XDT_I18N([@LINGUAS@])

dnl ***********************************
dnl *** Check for debugging support ***
dnl ***********************************
XDT_FEATURE_DEBUG()

dnl ************************
dnl *** Makefile outputs ***
dnl ************************
AC_CONFIG_FILES([
Makefile
src/Makefile
po/Makefile.in
])
AC_OUTPUT
